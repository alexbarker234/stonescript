// Weapon setups
var vigorWands = [
    ^"vigor wand +7", 
    ^"vigor wand +5"]
var aetherWands = [
    ^"aether wand *8", 
    ^"aether wand *7"]
var fireWands = [
    ^"fire wand *8", 
    ^"fire wand *7"]
var stunlock = [
    ^"stone hammer +9", 
    ^"aether hammer"]
// var unmakeSwords = [
//     ^"fire wand *8", 
//     ^"fire wand *7"]

// General vars
var shouldHarvest = false
var offhandMode = "heal"

// Globals
var hasEncountedBoss = false

// Find weak rune to use later
var weak_rune = stone
?foe = vigor
    weak_rune = poison
?foe = poison
    weak_rune = ice
?foe = ice
    weak_rune = fire
?foe = fire
    weak_rune = aether
?foe = aether
    weak_rune = vigor

?foe = boss
    >`0,6,State: @foe.state@ Time: @foe.time@

func equip_dash()
    ?item.GetCooldown("bash") <= 0
        equipR bashing shield
    :
        equipR dashing shield

func equip_set(set) 
    equipL @set[0]@
    equipR @set[1]@

func offhand_stone()
    ?offhandMode = "heal"
        //equipR ouroboros
        equipR vigor wand
    :?offhandMode = "xp"
        equipR experience
    :?offhandMode = "speed"
        equipR triskelion

func try_bardiche_ability()
    ?foe.distance <= 10 &
    ^item.GetCooldown("bardiche") <= 0 |
    ^item.GetCooldown("bardiche") > 870
        equip bardiche
        activate R

func try_blade_ability()
    ?foe.distance <= 10 &
    ^item.GetCooldown("blade") <= 0 &
    ^foe.count > 6
        equip blade
        activate R

func brew_and_use_health(hp_threshold)
    ?loc.begin
        brew tar
    ?hp < hp_threshold
        activate potion

func summon_fire()
    ?summon.count = 0
        equipR fire talisman
        ?item.GetCooldown("fire_talisman") <= 0
        ^ & item.CanActivate()
            activate R

// Default 
equipL triskelion
equipR ouroboros
?pickup.distance < 10
    equipL star

?loc = Rocky
    Rocky()
:?loc = Deadwood
    Deadwood()
:?loc = Caves
    Caves()
:?loc = Mushroom
    Mushroom()
:?loc = Halls
    Halls()
:?loc = Mine
    Mine()
:?loc = Icy
    Icy()
:?loc = Temple
    Temple()
    
// LOCATIONS
func Rocky()
    brew_and_use_health(7)

    ?foe.distance > 20
        return

    // Phase 1 - Weak to fire
    ?foe = dysangelos_bearer
        ?foe.distance > 10
            equip_dash()
        equip bardiche

    // Phase 2
    ?foe = dysangelos_elementalist
        // Use runes against current eye
        equipL @weak_rune@ wand
        equipR @weak_rune@ shield

    // Phase 3
    ?foe = dysangelos_perfected
        equip bardiche
        // yikers attack, TODO: dodge
        ?(hp + armor) < 31
            activate potion

    // slam his ass
    try_bardiche_ability()

// ===================
// Deadwood Canyon
// ===================
func Deadwood()
    // Experience Potion
    brew wood
    ?item.potion ! empty & time < 500
        activate potion

    // Harvesting
    ?harvest.distance < 10 & shouldHarvest
        equip hatchet

    // Boss
    ?foe = boss & foe.distance < 20
        Deadwood_Boss()
        return

    // General
    ?foe.distance < 20
        ?loc.stars <= 5
            ?foe.distance > 10
                equip_dash()
                return
            ?hp > maxhp - 5
                equipL stone sword +6
            :
                equipL vigor wand
            equipR experience  
            
        :
            equip heavy hammer 

func Deadwood_Boss()
    ?foe = poena
        equip_set(stunlock)
        ?foe.state = 32
            equipL mind
    :foe = tree_boss
        equip bardiche
        try_bardiche_ability()

// ===================
// Caves of Fear
// ===================
func Caves()
    brew_and_use_health(7)

    // Miniboss - whys it called that
    ?foe = cool_bat & foe.distance < 20
        Caves_MiniBoss()
        return
    // Boss
    ?foe = spider_boss & foe.distance < 20
        Caves_Boss()
        return

    // General
    ?foe.distance >= 16 & foe.distance < 20 
    ^& foe ! medium_bat
        equip_dash()

    // Fighting
    ?foe.distance <= 16
        ?loc.stars >= 8 | encounter.isElite | 
        ^foe.hp > 100
            equip bardiche
        :
            equipL vigor wand
            offhand_stone()

    try_blade_ability()

func Caves_MiniBoss()
    ?foe.distance > 10
        equip_dash()
    :
        equip_set(stunlock)

func Caves_Boss()
    ?loc.stars <= 5
        equip bardiche
        try_bardiche_ability()
    :
        // 
        ?foe.state = 142
            equipL mind
            return
        
        ?foe.distance > 5
            equip_dash()

        // try chill
        equip ice crossbow

// ===================
// Mushroom forest
// ===================
func Mushroom()
    brew_and_use_health(7)

    ?foe.distance >= 16 & foe.distance < 20
        equip_dash()

    ?foe.distance <= 16
        ?loc.stars < 5
            equip_set(vigorWands)
        : 
            equip bardiche

    try_blade_ability()

    // Boss
    ?foe = boss & foe.distance < 20
        ?foe.distance >= 10 
            equip_dash()
        :
            // ?foe = mushroom_boss_fat
            //     equip poison staff

            equip bardiche
            try_bardiche_ability()   

// ===================
// Haunted Halls
// ===================
func Halls()
    brew_and_use_health(7)

    ?foe.distance >= 16 & foe.distance < 20
        equip_dash()

    ?foe.distance <= 16
        ?foe.hp >= 100 & 
        ^foe ! immune_to_physical
            equip bardiche
        :
            equip_set(vigorWands)

    try_blade_ability()

    // Boss
    ?foe = boss & foe.distance < 20
        ?foe.distance >= 10 
            equip_dash()
        :
            equip bardiche
            try_bardiche_ability()   

// ===================
// Boiling Mine
// ===================       
func Mine()
    brew_and_use_health(7)

    ?foe.distance >= 16 & foe.distance < 20
        equip_dash()

    ?foe.distance <= 16
        ?loc.stars > 6
            equip bardiche
        :
            equip_set(aetherWands)
            ?foe = mine_walker
                equipL aether hammer
                equipR dashing shield
                
    try_blade_ability()

    // Mini Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bomb_cart
        Mine_Miniboss()

    // Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bronze_guardian
        Mine_Boss()

func Mine_Miniboss()
    equip_set(aetherWands)

    ?foe.distance < 10
        try_bardiche_ability()

        // RUH ROH RAGGY
        ?item.GetCooldown("bardiche") < 870 & 
        ^item.GetCooldown("bardiche") > 0
            equipL mind

func Mine_Boss() 
    ?hp = maxhp
        equip_set(aetherWands)
    :
        equip_set(vigorWands)

    var aiState = 0

    // Attack
    ?foe.state = 32
        ?foe.time > 15
            equipL mind
            equipR ouroboros
            aiState = 1
    // Cooldown - can melee
    :?foe.state = 33
        ?aiState = 1
            equip_dash()
            ?item.GetCooldown("dash") > 0
                aiState = 2
        :
            equip bardiche
            try_bardiche_ability()
    :
        aiState = 0

// ===================
// Icy Ridge
// ===================
func Icy()
    // TODO
    // aether swords to unmake ice pillars
    // fire swords could be better for boss
    brew_and_use_health(7)

    // General
    ?!hasEncountedBoss
        ?foe.distance >= 16 & foe.distance < 20
            equip_dash()
        :?foe.distance < 16
            equip bardiche

    ?foe = yeti
        hasEncountedBoss = true

    // Boss
    ?hasEncountedBoss & foe.distance < 20 
        Ice_Boss()
        
func Ice_Boss() 
    // can we fire shield the cold
    // shovel strat doesnt work on yellow?
    >`0,7,Dist:@foe.distance@

    // ?foe = ice_pillar | foe = ice_wall
    //     equip_set(unmake_swords)

    ?foe.armor > 0
        equip bardiche
    :
        // blow me
        ?foe.state = 133
            equipL fire wand
            equipR fire shield
            //equip_set(fireWands)
        :
            ?foe.distance <= 2
                equip bardiche
                try_bardiche_ability()
            :?foe.distance <= 5
                // Shovel moves close enough
                // to avoid everything
                equip shovel
            :?foe.distance > 5
                equip_dash()

// ===================
// TODO Temple 
// ===================
func Temple()
    return