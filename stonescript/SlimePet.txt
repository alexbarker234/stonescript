var slimeFrames = [
ascii
##.——.
#( • •)
asciiend,
ascii
###.—.
##(• •)
asciiend,
ascii
#______
(  • • ) 
asciiend
]

// config
var jumpLength = 10.0
var jumpHeight = 5
var slimeTargetZ
slimeTargetZ = pos.z + 1

var slimeTargetDist = 5;

// init vars
var slimeY = 0
var slimeZ = 0
var slimeX = 0
var slimeDrawY = 0
var lastPlayerX = 0
var lastPlayerZ = 0

// ai
var slimeAI = 0
var slimeState = 0
var slimeFrame = 0

// init slime
?loc.loop | loc.begin
  slimeY = 0
  slimeZ = 0
  slimeX = -11
  slimeDrawY = 0
  lastPlayerX = 0
  lastPlayerZ = 0 

  slimeState = 0
  slimeAI = 0
  slimeZ = slimeTargetZ

// chilling
?slimeState = 0
  slimeFrame = 0
  
  ?slimeX < -15
    slimeState = 1
    slimeAI = 0

// charge jump
:?slimeState = 1
  slimeAI++
  // switch slimeFrame every 5 ticks
  slimeFrame = math.FloorToInt(slimeAI / 3) % 2

  jumpLength = slimeX + slimeTargetDist

  ?slimeAI > 15
    slimeAI = 0
    slimeState = 2
// jump
:?slimeState = 2
  slimeFrame = 1

  slimeAI++
  // Sin arc jump
  slimeY = -math.Sin((slimeAI / jumpLength) * 
  ^math.pi) * jumpHeight
  slimeY = math.RoundToInt(slimeY)

  slimeX++

  ?slimeAI > jumpLength
    slimeState = 3
    slimeAI = 0
    slimeY = 0 
    slimeZ = slimeTargetZ

// land
:?slimeState = 3
  slimeFrame = 2
  slimeAI++

  ?slimeAI > 5
    slimeState = 0
    slimeAI = 0

// increment distance from player
slimeX = slimeX - (pos.x - lastPlayerX)

// tp if too far
?slimeX < -50
  slimeX = -20

slimeDrawY = slimeZ - pos.z + slimeY

// manage player vars 
lastPlayerX = pos.x
lastPlayerZ = pos.z

// draw relative to player
>o@slimeX@,@slimeDrawY@,#green,
^@slimeFrames[slimeFrame]@