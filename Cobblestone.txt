// Weapon setups
var vigorWands = [
    ^"vigor wand +11", 
    ^"vigor wand +10"]
var aetherWands = [
    ^"aether wand *8", 
    ^"aether wand *7"]
var fireWands = [
    ^"fire wand *8", 
    ^"fire wand *7"]
var stunlock = [
    ^"stone hammer +9", 
    ^"aether hammer"]
var unmakeSwords = [
    ^"aether sword dU +5", 
    ^"aether sword dU +4"]

// Data vars
var allAbilities = [
  "mind","hammer","bardiche","blade",
  "skeleton_arm","mask","fire_talisman",
  "cinderwisp","quarterstaff","dash","bash",
]
var abilityHUDNames = [
    "mind","ham","bard", "bfg","arm","mask",
    "tali","wisp","qtr","dash","bash"
]
var potions = ["experience","strength",
    "healing","lightning","invisibility",
    "cleansing","berserk","defensive","lucky",
    "vampiric"
]
var ingredients = [
    "wood + wood",
    "stone + stone",
    "tar + tar",
    "bronze + bronze",
    "wood + stone",
    "wood + tar",
    "wood + bronze",
    "stone + tar",
    "stone + bronze",
    "tar + bronze"
]
// Config vars
var shouldHarvest = false
var shouldPauseSkip = true

// Globals
var hasEncountedBoss = false
var hasPauseSkipped = false

// Find weak rune to use later
var weakRune = stone
?foe = vigor
    weakRune = poison
?foe = poison
    weakRune = ice
?foe = ice
    weakRune = fire
?foe = fire
    weakRune = aether
?foe = aether
    weakRune = vigor

// Helper Functions
func Between(value, min, max)
    return value >= min & value <= max

func ItemCD(itemName)
    return item.GetCooldown(itemName)

func FormatTime(timeVal)
    return time.FormatCasual(timeVal)

// Cross lookup
func xLookup(val, arr1, arr2)
    var index = arr1.IndexOf(val)
    ?index > -1
        return arr2[index]
    :
        return false

// Misc Functions
func EquipDash()
    ?ItemCD("bash") <= 0 
        equipR bashing shield
    :?ItemCD("dash") <= 0
        equipR dashing shield

func EquipSet(set) 
    equipL @set[0]@
    equipR @set[1]@

func TryBardicheAbility()
    ?foe.distance <= 10 &
    ^ItemCD("bardiche") <= 0 |
    ^ItemCD("bardiche") > 870
        equip bardiche
        activate R

func brewPot(potion)
    ?potion = "experience"
        brew wood + wood
    :?potion = "strength"
        brew stone + stone
    :?potion = "healing"
        brew tar + tar
    :?potion = "lightning"
        brew bronze + bronze
    :?potion = "invisibility"
        brew wood + stone
    :?potion = "cleansing"
        brew wood + tar
    :?potion = "berserk"
        brew wood + bronze
    :?potion = "defensive"
        brew stone + tar
    :?potion = "lucky"
        brew stone + bronze
    :?potion = "vampiric"
        brew tar + bronze

func TryBladeAbility()
    ?foe.distance <= 10 &
    ^ItemCD("blade") <= 0 &
    ^foe.count > 6
        equip blade
        activate R

// Double-screen BFG/Smite
var scrolling = 0
func DoubleBlade()
    var bladeReady = ItemCD("blade") <= 0
    ?bladeReady & foe.count > 3
        scrolling++
        screen.Next()
    :
        scrolling = 0
        screen.ResetOffset()
    ?bladeReady & 
    ^foe.distance < 25 & 
    ^(foe.count > 5 | scrolling > 15)
        equip blade
        activate R
        return true
    return scrolling > 0

func UseHealth(hp_threshold)
    ?loc.begin
        brew tar
    ?hp < hp_threshold
        activate potion

func FireTalisman()
    var cooldown = ItemCD("fire_talisman")
    ?summon.count = 0 | cooldown > 280
        equipR fire talisman
        ?cooldown <= 0
        ^ & item.CanActivate()
            activate R

func MaskAbility()
    ?ItemCD("mask") <= 0 
        equip mask
        activate R

func DoAnimCancel()
  var isTwoHanded = !item.left // bit jank
  var leftItem = item.left
  var rightItem = item.right
  
  // equip trash
  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0

  // re equip
  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

// LOCATIONS
func Rocky()
    UseHealth(7)

    ?foe.distance > 20
        return

    // Phase 1 - Weak to fire
    ?foe = dysangelos_bearer
        ?foe.distance > 10
            EquipDash()
        equip bardiche

    // Phase 2
    ?foe = dysangelos_elementalist
        // Use runes against current eye
        equipL @weakRune@ wand
        equipR @weakRune@ shield

    // Phase 3
    ?foe = dysangelos_perfected
        equip bardiche
        // yikers attack, TODO: dodge
        ?(hp + armor) < 31
            activate potion

    // slam his ass
    TryBardicheAbility()

// ===================
// Deadwood Canyon
// ===================
func Deadwood()
    // Experience Potion
    ?loc.stars <= 5
        brew wood
        ?item.potion ! empty & 
        ^Between(time, 60, 500)
            activate potion
    :
        UseHealth(7)

    // Harvesting
    ?harvest.distance < 10 & shouldHarvest
        equip hatchet

    // Boss
    ?foe = boss & foe.distance < 20
        DeadwoodBoss()
        return

    // General
    // TryBladeAbility()
    DoubleBlade()
    ?foe.distance < 20
        // Oro stone should heal enough
        ?loc.stars <= 5
            ?foe.distance > 10
                EquipDash()
                return
            equipL stone sword +6
            equipR experience           
        :
            equip heavy hammer 

func DeadwoodBoss()
    ?foe = poena
        EquipSet(stunlock)
        // ?foe.state = 32
        //     equipL mind
    :?foe = tree_boss
        equip bardiche
        TryBardicheAbility()

// ===================
// Caves of Fear
// ===================
func Caves()
    // TODO move next screen and
    // use the snake on bolesh 
    UseHealth(7)

    // Miniboss - whys it called that
    ?foe = cool_bat & foe.distance < 20
        CavesMiniBoss()
        return
    // Boss
    ?foe = spider_boss & foe.distance < 20
        CavesBoss()
        return

    // General
    ?foe.distance >= 16 & foe.distance < 20 
    ^& foe ! medium_bat
        EquipDash()

    // Fighting
    ?foe.distance <= 16
        ?loc.stars >= 8 | encounter.isElite | 
        ^foe.hp > 100
            equip bardiche
        :
            EquipSet(vigorWands)

    TryBladeAbility()

func CavesMiniBoss()
    ?foe.distance > 10
        EquipDash()
    :
        EquipSet(stunlock)

func CavesBoss()
    ?loc.stars <= 5
        equip bardiche
        TryBardicheAbility()
    :
        // 
        ?foe.state = 142
            equipL mind
            return
        
        ?foe.distance > 5
            EquipDash()

        // try chill
        equip ice crossbow

// ===================
// Mushroom forest
// ===================
func Mushroom()
    UseHealth(7)

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        ?loc.stars < 5
            EquipSet(vigorWands)
        : 
            equip bardiche

    TryBladeAbility()

    // Boss
    ?foe = boss & foe.distance < 20
        ?foe.distance >= 10 
            EquipDash()
        :
            // ?foe = mushroom_boss_fat
            //     equip poison staff

            equip bardiche
            TryBardicheAbility()   

// ===================
// Haunted Halls
// ===================
func Halls()
    UseHealth(7)

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        ?foe.hp >= 100 & 
        ^foe ! immune_to_physical
            equip bardiche
        :
            EquipSet(vigorWands)

    TryBladeAbility()

    // Boss
    ?foe = boss & foe.distance < 20
        ?foe.distance >= 10 
            EquipDash()
        :
            equip bardiche
            TryBardicheAbility()   

// ===================
// Boiling Mine
// ===================       
func Mine()
    UseHealth(7)

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        ?loc.stars > 6
            equip bardiche
        :
            EquipSet(aetherWands)
            ?foe = mine_walker
                equipL aether hammer
                equipR dashing shield
                
    TryBladeAbility()

    // Mini Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bomb_cart
        MineMiniboss()

    // Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bronze_guardian
        MineBoss()

func MineMiniboss()
    EquipSet(aetherWands)

    ?foe.distance < 10
        TryBardicheAbility()

        // RUH ROH RAGGY
        ?ItemCD("bardiche") < 870 & 
        ^ItemCD("bardiche") > 0
            equipL mind

func MineBoss() 
    ?hp = maxhp
        EquipSet(aetherWands)
    :
        EquipSet(vigorWands)

    var aiState = 0

    // Attack
    ?foe.state = 32
        ?foe.time > 15
            equipL mind
            equipR ouroboros
            aiState = 1
    // Cooldown - can melee
    :?foe.state = 33
        ?aiState = 1
            EquipDash()
            ?ItemCD("dash") > 0
                aiState = 2
        :
            equip bardiche
            TryBardicheAbility()
    :
        aiState = 0

// ===================
// Icy Ridge
// ===================
func Icy()
    brewPot("beserk")

    equipL triskelion
    equipR fire shield

    // Mark boss
    ?foe = yeti
        hasEncountedBoss = true

    // Unmake pillar
    ?(foe = ice_pillar | foe = ice_wall) & 
    ^foe.distance < 20
        EquipSet(unmakeSwords)
        return

    // Boss
    ?hasEncountedBoss & foe.distance < 20 
        IceBoss()
        return

    // General
    DoubleBlade()

    ?foe.distance < 20
        ?foe.distance >= 16 
            EquipDash()
            return
        equip bardiche

func IceBoss() 
    // ice sword until fully chilled then
    // fire sword
    >`0,7,Dist:@foe.distance@

    // intro state
    ?foe.armor > 0
        equip bardiche
        return

    ?item.potion ! empty
        activate potion

    // Pause skip
    ?shouldPauseSkip & !hasPauseSkipped
        hasPauseSkipped = true
        loc.Pause()

    // blowing
    ?foe.state = 133
        EquipSet(fireWands)
        return

    // Shovel strat
    ?loc.stars <= 5
        ?foe.distance <= 2
            equip bardiche
            TryBardicheAbility()
        :?foe.distance <= 5
            equip shovel
        :?foe.distance > 5
            EquipDash()
    // normal
    :
        ?foe.distance > 10
            EquipDash()
        :
            equip bardiche
            TryBardicheAbility()

// ===================
// Temple 
// ===================
func Temple()
    brewPot("berserk")

    // Boss
    ?foe = boss & foe.distance < 20
        TempleBoss()
        return

    // General
    DoubleBlade()
    ?foe.distance < 20
        ?foe.distance > 16
            EquipDash()
        :
            equip ice crossbow

func TempleBoss()
    // TODO double ice sword
    ?item.potion ! empty
        activate potion

    equip ice crossbow

func ColorCD(cd)
    ?cd > 0
        return "red"
    return "green"

func HUD()
    // Disable utility belt
    disable hud u 

    var bottom = screen.h - 2

    // CD HUD
    var x = screen.w - 10
    var x2 = screen.w - 5
    var y = 1
    for abi : allAbilities
        var cd = ItemCD(abi)     
        var text = xLookup(abi,allAbilities,
        ^abilityHUDNames)
        var textCol = ColorCD(cd)

        >`@x@,@y@,#@textCol@,
        ^@string.Capitalize(text)@

        >`@x2@,@y@,#@textCol@,
        ^@time.FormatDigital(cd)@
        y++

    // Boss state
    ?foe = boss
        >`0,6,State: @foe.state@ 
        ^Time: @foe.time@

    // Time
    var current = 
        ^FormatTime(time) + " " + 
        ^time + "F"
    var best = 
        ^FormatTime(loc.bestTime) + " " + 
        ^loc.bestTime + "F"
    var average = 
        ^FormatTime(loc.averageTime) + " " + 
        ^loc.averageTime + "F"

    >`0,@bottom - 2@,Cur:
    >`7,@bottom - 2@,@current@

    >`0,@bottom - 1@,Best:
    >`7,@bottom - 1@,@best@

    >`0,@bottom@,Avg:
    >`7,@bottom@,@average@


func Main()
    // Always run animation cancel
    DoAnimCancel()

    // Default 
    equipL triskelion
    equipR ouroboros
    ?pickup.distance < 10
        equipL star

    ?loc = Rocky
        Rocky()
    :?loc = Deadwood
        Deadwood()
    :?loc = Caves
        Caves()
    :?loc = Mushroom
        Mushroom()
    :?loc = Halls
        Halls()
    :?loc = Mine
        Mine()
    :?loc = Icy
        Icy()
    :?loc = Temple
        Temple()

    FireTalisman()

    HUD()

Main()