// Weapon setups
// Wands
var vigorWands = [
    ^"vigor wand +11", 
    ^"vigor wand +10"]
var aetherWands = [
    ^"aether wand *8", 
    ^"aether wand *7"]
var fireWands = [
    ^"fire wand *8", 
    ^"fire wand *7"]
var stunlock = [
    ^"stone hammer +9", 
    ^"aether hammer"]

// Swords
var unmakeSwords = [
    ^"aether sword dU +5", 
    ^"aether sword dU +4"]
var iceSwords = [
    ^"ice sword", 
    ^"ice sword -gold"]
var fireSwords = [
    ^"fire sword +7", 
    ^"fire sword +8"]

// Data vars
var allAbilities = [
  "mind","hammer","bardiche","blade",
  "skeleton_arm","mask","fire_talisman",
  "cinderwisp","quarterstaff","dash","bash",
]
var abilityHUDNames = [
    "mind","ham","bard", "bfg","arm","mask",
    "tali","wisp","qtr","dash","bash"
]
// Config vars
var shouldHarvest = false
var shouldPauseSkip = true
var xpFarm = false

// Globals
var hasEncountedBoss = false
var hasPauseSkipped = false
var aiState = 0

var foeDebuffs = ""
var smiteStack = 0
var chillStack = 0

func ResetGlobals()
    hasEncountedBoss = false
    hasPauseSkipped = false
    aiState = 0

func ResetGlobalsPerFrame()
    foeDebuffs = foe.debuffs.string
    smiteStack = 
    ^GetEffectStack(buffs.string,"smite")
    >`0,8, debuffs: @foeDebuffs@
    chillStack = 
    ^GetEffectStack(foeDebuffs,"debuff_chill")
    // Equip resets
    toEquipL = ""
    toEquipR = ""
    toEquip = ""
    shouldActivate = false
    
// Find weak rune to use later
var weakRune = stone
?foe = vigor
    weakRune = poison
?foe = poison
    weakRune = ice
?foe = ice
    weakRune = fire
?foe = fire
    weakRune = aether
?foe = aether
    weakRune = vigor

// Helper Functions
func Between(value, min, max)
    return value >= min & value <= max

func ItemCD(itemName)
    return item.GetCooldown(itemName)

func FormatTime(timeVal)
    return time.FormatCasual(timeVal)

// Item Equip Helper
var toEquipL = ""
var toEquipR = ""
var toEquip = ""
var shouldActivate = false

func SetEquipL(itemName)
    toEquipL = itemName

func SetEquipR(itemName)
    toEquipR = itemName

func SetEquip(itemName)
    toEquipL = ""
    toEquipR = ""
    toEquip = itemName

func ActivateItem()
    shouldActivate = true

func EquipItems()
    ?toEquipL
        equipL @toEquipL@
    ?toEquipR
        equipR @toEquipR@
    ?toEquip
        equip @toEquip@

func ActivateItems()
    ?shouldActivate & item.CanActivate()
        activate R

// Cross lookup
func xLookup(val, arr1, arr2)
    var index = arr1.IndexOf(val)
    ?index > -1
        return arr2[index]
    :
        return false

// Misc Functions
func EquipDash()
    ?ItemCD("bash") <= 0 
        SetEquipR("bashing shield")
    :?ItemCD("dash") <= 0
        SetEquipR("dashing shield")

func EquipSet(set) 
    SetEquipL(set[0])
    SetEquipR(set[1])

func GetEffectStack(str, uid)
    // Does not have
    ?!(str = uid)
        return 0
    // Split
    var parts = 
        ^string.Split(str, ":", ",", false)
    var i = parts.IndexOf(uid)
    // return duration for stun
    ?uid = "stun"
        return int.Parse(parts[i+2])
    // stacks
    return int.Parse(parts[i+1])

func TryBardicheAbility()
    var cooldown = ItemCD("bardiche")
    ?foe.distance <= 10 &
    ^cooldown <= 0 | cooldown > 870
        SetEquip("bardiche")
        ActivateItem()

func BrewPot(toBrew)
    ?!loc.begin
        return
    ?toBrew = "experience"
        brew wood + wood
    :?toBrew = "strength"
        brew stone + stone
    :?toBrew = "healing"
        brew tar + tar
    :?toBrew = "lightning"
        brew bronze + bronze
    :?toBrew = "invisibility"
        brew wood + stone
    :?toBrew = "cleansing"
        brew wood + tar
    :?toBrew = "berserk"
        brew wood + bronze
    :?toBrew = "defensive"
        brew stone + tar
    :?toBrew = "lucky"
        brew stone + bronze
    :?toBrew = "vampiric"
        brew tar + bronze

func TryBladeAbility()
    ?foe.distance <= 10 &
    ^ItemCD("blade") <= 0 &
    ^foe.count > 6
        SetEquip("blade")
        ActivateItem()

func QtrDash()
    ?foe.count ! 0
        return
    var cooldown = ItemCD("quarterstaff")
    ?cooldown <= 0 | cooldown > 175
        SetEquip("quarterstaff")
        ActivateItem()

// Double-screen BFG/Smite
var scrolling = 0
func DoubleBlade()
    var bladeReady = ItemCD("blade") <= 0
    ?bladeReady & foe.count > 3
        scrolling++
        screen.Next()
    :
        scrolling = 0
        screen.ResetOffset()
    ?bladeReady & 
    ^foe.distance < 25 & 
    ^(foe.count > 5 | scrolling > 15)
        SetEquip("blade")
        ActivateItem()
        return true
    return scrolling > 0

func UseHealth(hp_threshold)
    ?loc.begin
        brew tar
    ?hp < hp_threshold
        activate potion

func FireTalisman()
    var cooldown = ItemCD("fire_talisman")
    ?summon.count = 0 | cooldown > 130
        SetEquipR("fire talisman")
        ?cooldown <= 0
        ^ & item.CanActivate()
            ActivateItem()

func MaskAbility()
    ?ItemCD("mask") <= 0 
        SetEquip("mask")
        ActivateItem()

func RunAAC()
    ?item.left.state = 3
        equipL *0 wand //trash
        equipL @item.left@
    ?item.right.state = 3
        equipR *0 shield //trash
        equip @item.right@

// LOCATIONS
func Rocky()
    UseHealth(7)

    ?foe.distance > 20
        return

    MaskAbility()

    // Phase 1 - Weak to fire
    ?foe = dysangelos_bearer
        EquipSet(iceSwords)
        ?foe.distance > 10
            EquipDash()
            
    // Phase 2
    ?foe = dysangelos_elementalist
        // Use weak
        SetEquipL(weakRune + " wand")
        SetEquipR(weakRune + " shield")

    // Phase 3
    ?foe = dysangelos_perfected
        EquipSet(iceSwords)
        ?foe.distance > 10
            EquipDash()

        // yikers attack, TODO: dodge
        ?foe.state = 115 & foe.time > 70
            SetEquipR("mind")
            return
        ?(hp + armor) < 31
            activate potion

    // slam his ass
    TryBardicheAbility()

// For Phase 3 - gets strong to some elements
func FindDysaWeakness()
  ?foe.buffs.string ! "ice"
    return "ice"
  :?foe.buffs.string ! "fire"
    return "fire"
  :?foe.buffs.string ! "aether"
    return "aether"
  :?foe.buffs.string ! "vigor"
    return "vigor"
  :
    return "poison"

// ===================
// Deadwood Canyon
// ===================
func Deadwood()
    // Experience Potion
    ?loc.stars <= 5 & xpFarm
        brew wood
        ?item.potion ! empty & 
        ^Between(time, 60, 500)
            activate potion
    :
        UseHealth(7)

    SetEquipL("triskelion")
    SetEquipR("ice shield")
    ?pickup.distance < 10
        SetEquipR("star")

    // Harvesting
    ?harvest.distance < 10 & shouldHarvest
        SetEquip("hatchet")

    // Boss
    ?foe = boss & foe.distance < 20
        DeadwoodBoss()
        return

    // General
    DoubleBlade()
    ?foe.distance < 20     
        ?foe.distance > 10
            EquipDash()
        :
            SetEquip("ice staff")

func DeadwoodBoss()
    ?foe = poena
        EquipSet(stunlock)
        // ?foe.state = 32
        //     SetEquipL("mind")
    :?foe = tree_boss
        SetEquip("bardiche")
        TryBardicheAbility()

// ===================
// Caves of Fear
// ===================
func Caves()
    // TODO move next screen and
    // use the snake on bolesh 
    UseHealth(7)

    // Miniboss - whys it called that
    ?foe = cool_bat & foe.distance < 20
        CavesMiniBoss()
        return
    // Boss
    ?foe = spider_boss & foe.distance < 20
        CavesBoss()
        return

    // General
    DoubleBlade()

    ?foe.distance >= 16 & foe.distance < 20 
    ^& foe ! medium_bat
        EquipDash()

    // Fighting
    ?foe.distance <= 16
        ?loc.stars >= 8 | encounter.isElite | 
        ^foe.hp > 100
            SetEquip("bardiche")
        :
            EquipSet(vigorWands)

func CavesMiniBoss()
    ?foe.distance > 10
        EquipDash()
    :
        EquipSet(stunlock)

func CavesBoss()
    ?loc.stars <= 5
        SetEquip("bardiche")
        TryBardicheAbility()
    :
        // 
        ?foe.state = 142
            SetEquipL("mind")
            return
        
        ?foe.distance > 5
            EquipDash()

        // try chill
        SetEquip("ice crossbow")

// ===================
// Mushroom forest
// ===================
func Mushroom()
    UseHealth(7)

    SetEquipL("triskelion")
    SetEquipR("poison shield")

    // Boss
    ?foe = boss & foe.distance < 20
        MushroomBoss()
        return
    DoubleBlade()

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        SetEquip("ice staff")

func MushroomBoss()
    ?foe.distance >= 10 
        EquipDash()
        return
    
    ?foe ! mushroom_boss
        QtrDash()

    // Enoki
    ?foe = mushroom_boss_skinny
        MaskAbility()
        
    // ?foe = mushroom_boss_fat
    //     SetEquip("poison staff")

    EquipSet(iceSwords)
    TryBardicheAbility()   

// ===================
// Haunted Halls
// ===================
func Halls()
    ?loc.stars <= 10
        BrewPot("beserk")
    :
        UseHealth(7)

    SetEquipL("triskelion")
    SetEquipR("vigor shield")

    // Boss
    ?foe = pallas & foe.distance < 20
        HallsBoss()
        return

    // General
    DoubleBlade()

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16 
        SetEquip("ice staff")

func HallsBoss()
    ?item.potion ! empty & loc.stars <= 10
        activate potion

    MaskAbility()

    // Pause skip
    ?shouldPauseSkip & !hasPauseSkipped &
    ^foe = legless
        hasPauseSkipped = true
        ui.OpenInv()
        return

    ?foe.distance >= 10 
        EquipDash()
        return

    ?chillStack < 6
        EquipSet(iceSwords)
    :
        EquipSet(vigorWands)

    // deal with skellys
    ?foe.count > 1
        SetEquip("bardiche")

    TryBardicheAbility()   

// ===================
// Boiling Mine
// ===================       
func Mine()
    BrewPot("beserk")

    SetEquipL("triskelion")
    SetEquipR("aether shield")
    ?pickup.distance < 10
        SetEquipR("star")

    // Mini Boss
    ?foe = explode
        MineMiniboss()
        return

    // Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bronze_guardian
        MineBoss()
        return

    // General
    DoubleBlade()

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance < 16
        ?foe.GetCount(10) = 1 & smiteStack = 0
            // todo aether swords
            EquipSet(iceSwords)
        :
            SetEquip("ice staff")
                
func MineMiniboss()
    ?foe.distance <= 16 & foe.distance >= 11
        SetEquipR("dash") // no bash
    ?foe.distance < 8
        SetEquipR("mind")

func MineBoss() 
    ?item.potion ! empty
        activate potion

    MaskAbility()

    // Attack
    ?foe.state = 32
        ?foe.time > 15
            SetEquipL("mind")
            SetEquipR("aether shield")
            aiState = 1
    // Cooldown - can melee
    :?foe.state = 33
        ?aiState = 1
            EquipDash()
            ?ItemCD("dash") > 0 |
            ^ItemCD("bash") > 0
                aiState = 2
        :
            EquipSet(unmakeSwords)
            TryBardicheAbility()
    :
        aiState = 0
        // todo: aether crossbow speed enchant
        EquipSet(aetherWands)


// ===================
// Icy Ridge
// ===================
func Icy()
    BrewPot("beserk")

    SetEquipL("triskelion")
    SetEquipR("fire shield")

    // Mark boss
    ?foe = yeti
        hasEncountedBoss = true

    // Unmake pillar
    ?(foe = ice_pillar | foe = ice_wall) & 
    ^foe.distance < 20
        EquipSet(unmakeSwords)
        return

    // Boss
    ?hasEncountedBoss & foe.distance < 20 
        IceBoss()
        return

    // General
    DoubleBlade()

    ?foe.distance < 20
        ?foe.distance >= 16 
            EquipDash()
            return
        ?smiteStack = 0
            EquipSet(fireSwords)
            return
        SetEquip("ice staff")

func IceBoss() 
    ?item.potion ! empty
        activate potion

    // intro state
    ?foe.armor > 0
        ?foe.distance > 10
            EquipDash() 
        :
            EquipSet(fireSwords)
        return

    // Pause skip
    ?shouldPauseSkip & !hasPauseSkipped
        hasPauseSkipped = true
        ui.OpenInv()
        return

    MaskAbility()

    // blowing
    ?foe.state = 133
        EquipSet(fireWands)
        return
    >`0,9,@chillStack@
    // General strat
    ?foe.distance > 10
        EquipDash()
    :
        ?chillStack < 6
            EquipSet(iceSwords)
        :
            >`0,9,@chillStack@ - fire
            EquipSet(fireSwords)
        TryBardicheAbility()

// ===================
// Temple 
// ===================
func Temple()
    BrewPot("berserk")

    // Boss
    ?foe = boss & foe.distance < 20
        TempleBoss()
        return

    // General
    DoubleBlade()
    ?foe.distance < 20
        ?foe.distance > 16
            EquipDash()
        :
            SetEquip("ice crossbow")

func TempleBoss()
    // TODO double ice sword
    ?item.potion ! empty
        activate potion

    SetEquip("ice crossbow")

func ColorCD(cd)
    ?cd > 0
        return "red"
    return "green"

func HUD()
    // Disable utility belt
    disable hud u 

    var bottom = screen.h - 2

    // CD HUD
    var x = screen.w - 10
    var x2 = screen.w - 5
    var y = 1
    for abi : allAbilities
        var cd = ItemCD(abi)     
        var text = xLookup(abi,allAbilities,
        ^abilityHUDNames)
        var textCol = ColorCD(cd)

        >`@x@,@y@,#@textCol@,
        ^@string.Capitalize(text)@

        >`@x2@,@y@,#@textCol@,
        ^@time.FormatDigital(cd)@
        y++

    // Boss state
    ?foe = boss
        >`0,6,State: @foe.state@ 
        ^Time: @foe.time@
    >`0,7,Dist: @foe.distance@
    // Time
    var current = 
        ^FormatTime(totaltime) + " " + 
        ^totaltime + "F"
    var best = 
        ^FormatTime(loc.bestTime) + " " + 
        ^loc.bestTime + "F"
    var average = 
        ^FormatTime(loc.averageTime) + " " + 
        ^loc.averageTime + "F"

    >`0,@bottom - 2@,Cur:
    >`7,@bottom - 2@,@current@

    >`0,@bottom - 1@,Best:
    >`7,@bottom - 1@,@best@

    >`0,@bottom@,Avg:
    >`7,@bottom@,@average@

func Main()
    // Always run animation cancel
    RunAAC()

    ResetGlobalsPerFrame()
    ?loc.begin | loc.loop
        ResetGlobals()

    // Default 
    SetEquipL("triskelion")
    SetEquipR("ouroboros")
    ?pickup.distance < 10
        SetEquipL("star")

    // always dash
    QtrDash()

    ?loc = Rocky
        Rocky()
    :?loc = Deadwood
        Deadwood()
    :?loc = Caves
        Caves()
    :?loc = Mushroom
        Mushroom()
    :?loc = Halls
        Halls()
    :?loc = Mine
        Mine()
    :?loc = Icy
        Icy()
    :?loc = Temple
        Temple()

    // No deadwood - poena bites 
    :?loc ! Deadwood
        FireTalisman()

    EquipItems()
    ActivateItems()

    HUD()

Main()