// Weapon setups
var vigorWands = [
    ^"vigor wand +7", 
    ^"vigor wand +5"]
var aetherWands = [
    ^"aether wand *8", 
    ^"aether wand *7"]
var fireWands = [
    ^"fire wand *8", 
    ^"fire wand *7"]
var stunlock = [
    ^"stone hammer +9", 
    ^"aether hammer"]
// var unmakeSwords = [
//     ^"fire wand *8", 
//     ^"fire wand *7"]

// General vars
var shouldHarvest = false

// Globals
var hasEncountedBoss = false

// Find weak rune to use later
var weakRune = stone
?foe = vigor
    weakRune = poison
?foe = poison
    weakRune = ice
?foe = ice
    weakRune = fire
?foe = fire
    weakRune = aether
?foe = aether
    weakRune = vigor

?foe = boss
    >`0,6,State: @foe.state@ Time: @foe.time@

// Helper Functions
func Between(value, min, max)
    return value >= min & value <= max

// Misc Functions
func EquipDash()
    ?item.GetCooldown("bash") <= 0
        equipR bashing shield
    :
        equipR dashing shield

func EquipSet(set) 
    equipL @set[0]@
    equipR @set[1]@

func TryBardicheAbility()
    ?foe.distance <= 10 &
    ^item.GetCooldown("bardiche") <= 0 |
    ^item.GetCooldown("bardiche") > 870
        equip bardiche
        activate R

func TryBladeAbility()
    ?foe.distance <= 10 &
    ^item.GetCooldown("blade") <= 0 &
    ^foe.count > 6
        equip blade
        activate R

func UseHealth(hp_threshold)
    ?loc.begin
        brew tar
    ?hp < hp_threshold
        activate potion

func FireTalisman()
    ?summon.count = 0
        equipR fire talisman
        ?item.GetCooldown("fire_talisman") <= 0
        ^ & item.CanActivate()
            activate R

func DoAnimCancel()
  var isTwoHanded = !item.left // bit jank
  var leftItem = item.left
  var rightItem = item.right
  
  // equip trash
  ?item.right.state = 3
    equipR shield *0 +0
  ?item.left.state = 3
    equipL wand *0 +0

  // re equip
  ?isTwoHanded
    ?rightItem
      equip @rightItem@
    :?leftItem
      equip @leftItem@
  :
    ?rightItem
      equipR @rightItem@
    ?leftItem
      equipL @leftItem@

// Always run animation cancel
DoAnimCancel()

// Default 
equipL triskelion
equipR ouroboros
?pickup.distance < 10
    equipL star

?loc = Rocky
    Rocky()
:?loc = Deadwood
    Deadwood()
:?loc = Caves
    Caves()
:?loc = Mushroom
    Mushroom()
:?loc = Halls
    Halls()
:?loc = Mine
    Mine()
:?loc = Icy
    Icy()
:?loc = Temple
    Temple()

// LOCATIONS
func Rocky()
    UseHealth(7)

    ?foe.distance > 20
        return

    // Phase 1 - Weak to fire
    ?foe = dysangelos_bearer
        ?foe.distance > 10
            EquipDash()
        equip bardiche

    // Phase 2
    ?foe = dysangelos_elementalist
        // Use runes against current eye
        equipL @weakRune@ wand
        equipR @weakRune@ shield

    // Phase 3
    ?foe = dysangelos_perfected
        equip bardiche
        // yikers attack, TODO: dodge
        ?(hp + armor) < 31
            activate potion

    // slam his ass
    TryBardicheAbility()

// ===================
// Deadwood Canyon
// ===================
func Deadwood()
    // Experience Potion
    ?loc.stars <= 5
        brew wood
        ?item.potion ! empty & 
        ^Between(time, 60, 500)
            activate potion
    :
        UseHealth(7)

    // Harvesting
    ?harvest.distance < 10 & shouldHarvest
        equip hatchet

    // Boss
    ?foe = boss & foe.distance < 20
        DeadwoodBoss()
        return

    // General
    TryBladeAbility()
    ?foe.distance < 20
        // Oro stone should heal enough
        ?loc.stars <= 5
            ?foe.distance > 10
                EquipDash()
                return
            equipL stone sword +6
            equipR experience           
        :
            equip heavy hammer 

func DeadwoodBoss()
    ?foe = poena
        EquipSet(stunlock)
        ?foe.state = 32
            equipL mind
    :?foe = tree_boss
        equip bardiche
        TryBardicheAbility()

// ===================
// Caves of Fear
// ===================
func Caves()
    UseHealth(7)

    // Miniboss - whys it called that
    ?foe = cool_bat & foe.distance < 20
        CavesMiniBoss()
        return
    // Boss
    ?foe = spider_boss & foe.distance < 20
        CavesBoss()
        return

    // General
    ?foe.distance >= 16 & foe.distance < 20 
    ^& foe ! medium_bat
        EquipDash()

    // Fighting
    ?foe.distance <= 16
        ?loc.stars >= 8 | encounter.isElite | 
        ^foe.hp > 100
            equip bardiche
        :
            EquipSet(vigorWands)

    TryBladeAbility()

func CavesMiniBoss()
    ?foe.distance > 10
        EquipDash()
    :
        EquipSet(stunlock)

func CavesBoss()
    ?loc.stars <= 5
        equip bardiche
        TryBardicheAbility()
    :
        // 
        ?foe.state = 142
            equipL mind
            return
        
        ?foe.distance > 5
            EquipDash()

        // try chill
        equip ice crossbow

// ===================
// Mushroom forest
// ===================
func Mushroom()
    UseHealth(7)

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        ?loc.stars < 5
            EquipSet(vigorWands)
        : 
            equip bardiche

    TryBladeAbility()

    // Boss
    ?foe = boss & foe.distance < 20
        ?foe.distance >= 10 
            EquipDash()
        :
            // ?foe = mushroom_boss_fat
            //     equip poison staff

            equip bardiche
            TryBardicheAbility()   

// ===================
// Haunted Halls
// ===================
func Halls()
    UseHealth(7)

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        ?foe.hp >= 100 & 
        ^foe ! immune_to_physical
            equip bardiche
        :
            EquipSet(vigorWands)

    TryBladeAbility()

    // Boss
    ?foe = boss & foe.distance < 20
        ?foe.distance >= 10 
            EquipDash()
        :
            equip bardiche
            TryBardicheAbility()   

// ===================
// Boiling Mine
// ===================       
func Mine()
    UseHealth(7)

    ?foe.distance >= 16 & foe.distance < 20
        EquipDash()

    ?foe.distance <= 16
        ?loc.stars > 6
            equip bardiche
        :
            EquipSet(aetherWands)
            ?foe = mine_walker
                equipL aether hammer
                equipR dashing shield
                
    TryBladeAbility()

    // Mini Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bomb_cart
        MineMiniboss()

    // Boss
    ?foe = boss & foe.distance < 30 &
    ^foe = bronze_guardian
        MineBoss()

func MineMiniboss()
    EquipSet(aetherWands)

    ?foe.distance < 10
        TryBardicheAbility()

        // RUH ROH RAGGY
        ?item.GetCooldown("bardiche") < 870 & 
        ^item.GetCooldown("bardiche") > 0
            equipL mind

func MineBoss() 
    ?hp = maxhp
        EquipSet(aetherWands)
    :
        EquipSet(vigorWands)

    var aiState = 0

    // Attack
    ?foe.state = 32
        ?foe.time > 15
            equipL mind
            equipR ouroboros
            aiState = 1
    // Cooldown - can melee
    :?foe.state = 33
        ?aiState = 1
            EquipDash()
            ?item.GetCooldown("dash") > 0
                aiState = 2
        :
            equip bardiche
            TryBardicheAbility()
    :
        aiState = 0

// ===================
// Icy Ridge
// ===================
func Icy()
    // TODO
    // aether swords to unmake ice pillars
    // fire swords could be better for boss
    UseHealth(7)

    // General
    ?!hasEncountedBoss
        ?foe.distance >= 16 & foe.distance < 20
            EquipDash()
        :?foe.distance < 16
            equip bardiche

    ?foe = yeti
        hasEncountedBoss = true

    // Boss
    ?hasEncountedBoss & foe.distance < 20 
        IceBoss()
        
func IceBoss() 
    // can we fire shield the cold
    // shovel strat doesnt work on yellow?
    >`0,7,Dist:@foe.distance@

    // ?foe = ice_pillar | foe = ice_wall
    //     EquipSet(unmake_swords)

    ?foe.armor > 0
        equip bardiche
    :
        // blow me
        ?foe.state = 133
            equipL fire wand
            equipR fire shield
            //EquipSet(fireWands)
        :
            ?foe.distance <= 2
                equip bardiche
                TryBardicheAbility()
            :?foe.distance <= 5
                // Shovel moves close enough
                // to avoid everything
                equip shovel
            :?foe.distance > 5
                EquipDash()

// ===================
// TODO Temple 
// ===================
func Temple()
    return